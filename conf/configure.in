dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.59)
AC_INIT([MRNet],[5.0.1],[mrnet@cs.wisc.edu],[mrnet],[http://www.paradyn.org/mrnet/])
AC_CONFIG_SRCDIR(src/Network.C)
AC_CONFIG_AUX_DIR(conf)

abs_srcdir=`cd $srcdir && pwd`

dnl === C++ COMPILERS ===
dnl TODO: figure out dlopen()/shared object idiosyncracies when using non-GNU compilers
AC_PROG_CC(clang xlc_r cc gcc pgcc icc)
AC_PROG_CXX(clang++ xlC_r CC g++ pgCC icpc)
AC_LANG(C++)

dnl === CONFIGURE OPTIONS
AC_ARG_WITH(startup,
            [AS_HELP_STRING([--with-startup=METHOD],
                            [Tree instantiation method - "ssh" (default), "cray", "cray-cti", "libi"])],
            [MRNET_STARTUP_METHOD="${withval}"],
            [MRNET_STARTUP_METHOD="ssh"])

AC_ARG_WITH(alpstoolhelp-lib,
            [AS_HELP_STRING([--with-alpstoolhelp-lib=PATH],
                            [(Cray only) Absolute path to directory containing libalps library])],
            [CRAY_ATH_LIBDIR="${withval}"],
            [CRAY_ATH_LIBDIR=""])
    
AC_ARG_WITH(alpstoolhelp-inc,
            [AS_HELP_STRING([--with-alpstoolhelp-inc=PATH],
                            [(Cray only) Absolute path to directory containing libalps.h])],
            [CRAY_ATH_INCDIR="${withval}"],
            [CRAY_ATH_INCDIR=""])

AC_ARG_WITH(craycti-lib,
            [AS_HELP_STRING([--with-craycti-lib=PATH],
                            [(Cray only) Absolute path to directory containing CTI library])],
            [CRAY_CTI_LIBDIR="${withval}"],
            [CRAY_CTI_LIBDIR=""])

AC_ARG_WITH(craycti-inc,
            [AS_HELP_STRING([--with-craycti-inc=PATH],
                            [(Cray only) Absolute path to directory containing CTI headers])],
            [CRAY_CTI_INCDIR="${withval}"],
            [CRAY_CTI_INCDIR=""])

AC_ARG_WITH(boost,
            [AS_HELP_STRING([--with-boost=PATH],
                            [Absolute path to installation of Boost C++ libraries (note: specify the path to the directory containing "include" and "lib" sub-directories)])],
            [BOOST_INSTALL_DIR="${withval}"],
            [BOOST_INSTALL_DIR=""])

AC_ARG_WITH(libi-startup,
            [AS_HELP_STRING([--with-libi-startup=METHOD],
                            [Tree instantiation method - "ssh" (default), "slurm"])],
            [LIBI_STARTUP_METHOD="${withval}"],
            [LIBI_STARTUP_METHOD="ssh"])
AC_SUBST(LIBI_STARTUP_METHOD)

AC_ARG_WITH(libi,
			[AS_HELP_STRING([--with-libi=PATH],
							[Absolute path to the installation of LIBI C++ libraries (note: specify the path to the directory containing "include" and "lib" sub-directories)])],
			[LIBI_INSTALL_DIR="${withval}"],
			[LIBI_INSTALL_DIR="${abs_srcdir}/external/libi"])
AC_SUBST(LIBI_INSTALL_DIR)

AC_ARG_WITH(launchmon,
			[AS_HELP_STRING([--with-launchmon=PATH],
				[Absolute path to installation of LaunchMON])],
			[LMON_IDIR="${withval}"],
			[LMON_IDIR="${LIBI_INSTALL_DIR}/lmon"])
AC_SUBST(LMON_IDIR)

AC_ARG_WITH(expat,
            [AS_HELP_STRING([--with-expat=PATH],
                            [Absolute path to installation of EXPAT libraries (note: specify the path to the directory containing "include" and "lib" sub-directories)])],
            [EXPAT_DIR="${withval}"],
            [EXPAT_DIR=""]) 

if test "x$EXPAT_DIR" = "x" ; then
   EXPAT_LIB=""
else
   EXPAT_LIB="-L$EXPAT_DIR/lib"
fi 

AC_ARG_ENABLE(shared,
    [AS_HELP_STRING([--enable-shared],[Build shared object version of mrnet library (default off)])],
    [ if test "$enableval" = "no" ; then
       BUILDSHARED="no"
   else
       BUILDSHARED="yes"
   fi],
    [ BUILDSHARED="no"] )
AC_SUBST(BUILDSHARED)

AC_ARG_ENABLE(verbosebuild,
    [AS_HELP_STRING([--enable-verbosebuild],[Display commands during build (default off)])],
    [ if test "$enableval" = "no" ; then
       VERBOSEMAKE=".SILENT:"
   else
       VERBOSEMAKE=""
   fi],
   [VERBOSEMAKE=".SILENT:"])
AC_SUBST(VERBOSEMAKE)

AC_ARG_ENABLE(debug,
    [AS_HELP_STRING([--enable-debug],[Enable debug mode options (default off)])],
    [ if test "$enableval" = "no" ; then
       debug_mode="no"
   else
       debug_mode="yes"
   fi],
    [ debug_mode="no" ])

AC_ARG_ENABLE(ltwt-threadsafe,
    [AS_HELP_STRING([--enable-ltwt-threadsafe],[Enable the thread-safe version of the lightweight backends (default off)])],
    [ if test "$enableval" = "no" ; then
       LTWT_THREADSAFE="no"
   else
       LTWT_THREADSAFE="yes"
   fi],
    [ LTWT_THREADSAFE="no" ])

AC_ARG_WITH(purify,
    [AS_HELP_STRING([--with-purify],[Use purify for memory debugging])],
    [ if test "${withval}" = yes; then
          AC_PATH_PROG(PURIFY, purify)
      else
          PURIFY="${withval}"
      fi
    ])
AC_SUBST(PURIFY)


dnl === Checks for header files.
AC_CHECK_HEADERS([assert.h errno.h fcntl.h limits.h netdb.h signal.h stddef.h stdlib.h stdio.h string.h unistd.h arpa/inet.h netinet/in.h sys/ioctl.h sys/socket.h sys/sockio.h sys/time.h])
AC_HEADER_STDBOOL

dnl === Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T

dnl === Checks for library functions.
AC_FUNC_FORK
AC_CHECK_FUNCS([gethostbyname socket strchr strerror])

dnl === Check for required programs
AC_PROG_MAKE_SET
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_YACC
AC_PROG_LN_S

AC_PATH_PROG(GREP, grep)
AC_SUBST(GREP)

AC_PATH_PROG(CP, cp)
AC_SUBST(CP)

AC_PATH_PROG(RM, rm)
AC_SUBST(RM)

AC_PATH_PROG(FLEX, flex, flex_not_found)
if test "$FLEX" = "flex_not_found" ; then
    AC_MSG_WARN([flex not found in \$PATH, is it installed?])
fi
AC_SUBST(FLEX)

dnl === Initial settings ===
LIBS="-lm -lpthread -ldl"
LIBS_LIGHTWEIGHT="-lm"
LIBS_LOWFAT=""

EXTRA_LIBS=""
EXTRA_LIBS_SO=""
EXTRA_LIBS_LIGHTWEIGHT=""
EXTRA_LIBS_LOWFAT=""

dnl === Check for Boost C++ shared-ptr ===
HAVE_BOOST="no"
HAVE_USER_BOOST="no"
HAVE_BOOST_TIMER="no"
BOOST_INC=""
BOOST_LIB=""
if test "x$BOOST_INSTALL_DIR" = "x" ; then
    AC_CHECK_HEADERS([boost/shared_ptr.hpp],[HAVE_BOOST="yes"])
    AC_CHECK_HEADERS([boost/timer/timer.hpp], [HAVE_BOOST_TIMER="yes"])
else
    BOOST_INC="-I$BOOST_INSTALL_DIR/include"
    BOOST_LIB="-L$BOOST_INSTALL_DIR/lib"
    SAVE_CXXFLAGS="$CXXFLAGS"
    CXXFLAGS="$CXXFLAGS $BOOST_INC"
    AC_CHECK_HEADERS([boost/shared_ptr.hpp],[HAVE_USER_BOOST="yes"])
    AC_CHECK_HEADERS([boost/timer/timer.hpp], [HAVE_BOOST_TIMER="no"])
    CXXFLAGS="$SAVE_CXXFLAGS" 
fi

if test $HAVE_USER_BOOST = "yes" ; then
    CXXFLAGS="$CXXFLAGS $BOOST_INC"
    LIBS="$LIBS $BOOST_LIB"
    HAVE_BOOST="yes"
fi

if test $HAVE_BOOST_TIMER = "yes" ; then
    CXXFLAGS="$CXXFLAGS -DUSE_BOOST_TIMER"
    LIBS="$LIBS -lboost_system -lboost_timer"
fi

AC_SUBST(HAVE_BOOST)




dnl === Get necessary macros
STDCFLAGS="-D__STDC_LIMIT_MACROS -D__STDC_CONSTANT_MACROS -D__STDC_FORMAT_MACROS"
CXXFLAGS="$CXXFLAGS $STDCFLAGS"
CFLAGS="$CFLAGS $STDCFLAGS"

dnl === Ignore user-requested debug/optimization levels
CXXFLAGS=`echo $CXXFLAGS | sed 's/-g //; s/-O //; s/-O1 //; s/-O2 //; s/-O3 //'`
CFLAGS=`echo $CFLAGS | sed 's/-g //; s/-O //; s/-O1 //; s/-O2 //; s/-O3 //'`

dnl === SETTING MRNET PLATFORM VARIABLES ===

dnl MRNET_ARCH is host returned by config.guess with
dnl all `-` and `.` characters replaced by `_` so that a
dnl -DMRNET_ARCH can be passed to the compiler
dnl without it complaining

AC_CANONICAL_HOST
changequote(<<, >>) dnl - temporarily disable [] as quote
MRNET_ARCH=`echo $host | sed 's/[-|.]/_/g'`
MRNET_CPU=$host_cpu
MRNET_OS=`echo $host_os | sed 's/[-0-9].*//'`
MRNET_OSVER=`echo $host_os | sed 's/[-|.]/_/g'`
MRNET_ROOT=$abs_srcdir
MRNET_VERSION=$PACKAGE_VERSION
MRNET_VERSION_MAJOR=`echo $MRNET_VERSION | sed -e 's/\..*//'`
MRNET_VERSION_MINOR=`echo $MRNET_VERSION | sed -e 's/^[0-9]*\.//' | sed -e 's/\..*//'`
MRNET_VERSION_REVISION=`echo $MRNET_VERSION | sed -e 's/^[0-9]*\.[0-9]*\.//' | sed -e 's/\..*//'`
changequote([, ]) dnl - restore [] as quote

AC_DEFINE([COMMNODE_EXE], "mrnet_commnode", [Set to default path of mrnet_commnode executable.])
AC_SUBST(MRNET_ARCH)
AC_SUBST(MRNET_OS)
AC_SUBST(MRNET_ROOT)
AC_SUBST(MRNET_VERSION)
AC_SUBST(MRNET_VERSION_MAJOR)
AC_SUBST(MRNET_VERSION_MINOR)
AC_SUBST(MRNET_VERSION_REVISION)

CXXFLAGS="$CXXFLAGS -Darch_$MRNET_CPU"
CFLAGS="$CFLAGS -Darch_$MRNET_CPU"

dnl === COMPILER-SPECIFIC OPTIONS ===

AX_COMPILER_VENDOR

CXXDEBUGFLAGS="-g"
CDEBUGFLAGS="-g"

CXXOPTFLAGS="-O2"
COPTFLAGS="-O2"

COMPILER_TYPE=$ax_cv_[]_AC_LANG_ABBREV[]_compiler_vendor

if test $COMPILER_TYPE = "gnu" ; then

    CXXFLAGS="$CXXFLAGS -Dcompiler_gnu -fno-default-inline -fPIC"
    CFLAGS="$CFLAGS -Dcompiler_gnu -fPIC"

    SOFLAGS="$SOFLAGS -fPIC -shared"
    if test $MRNET_OS = "linux" ; then
        SOFLAGS="$SOFLAGS -rdynamic"
    fi

    C_AS_CPLUSPLUS="-x c++"

    AC_PATH_PROG(AR, ar)
    AC_SUBST(AR)
    ARFLAGS="crv"
    AC_SUBST(ARFLAGS)

elif test $COMPILER_TYPE = "clang"; then
    
    CXXFLAGS="$CXXFLAGS -Dcompiler_clang -fPIC"
    CFLAGS="$CFLAGS -Dcompiler_clang -fPIC"

    SOFLAGS="$SOFLAGS -fPIC -shared"
    if test $MRNET_OS = "linux" ; then
        SOFLAGS="$SOFLAGS -rdynamic"
    fi

    C_AS_CPLUSPLUS="-x c++"

    AC_PATH_PROG(AR, ar)
    AC_SUBST(AR)
    ARFLAGS="crv"
    AC_SUBST(ARFLAGS)

elif test $COMPILER_TYPE = "ibm" ; then

    CXXFLAGS="$CXXFLAGS -Dcompiler_ibm"
    CFLAGS="$CFLAGS -Dcompiler_ibm"

    SOFLAGS="$SOFLAGS -G"

    C_AS_CPLUSPLUS="-+"

    AC_PATH_PROG(AR, ar)
    AC_SUBST(AR)
    ARFLAGS="crv"
    AC_SUBST(ARFLAGS)

elif test $COMPILER_TYPE = "sun" ; then

    CXXFLAGS="$CXXFLAGS -Dcompiler_sun -KPIC"
    CFLAGS="$CFLAGS -Dcompiler_sun -KPIC"

    CXXOPTFLAGS="-xO2"
    COPTFLAGS="-xO2"

    CXXDEBUGFLAGS="-g -xO0"
    CDEBUGFLAGS="-g -xO0"

    SOFLAGS="$SOFLAGS -G"

    AC_PATH_PROG(AR, CC)
    AC_SUBST(AR)
    ARFLAGS="-xar -o"
    AC_SUBST(ARFLAGS)

elif test $COMPILER_TYPE = "portland" ; then

    CXXFLAGS="$CXXFLAGS -Dcompiler_pgi -D_GNU_SOURCE -fPIC"
    CFLAGS="$CFLAGS -Dcompiler_pgi -D_GNU_SOURCE -fPIC"

    CXXDEBUGFLAGS="-g -O0"
    CDEBUGFLAGS="-g -O0"

    PGI_OLD_TEMPLATES="no"
    AC_SUBST(PGI_OLD_TEMPLATES)

    SOFLAGS="$SOFLAGS -fPIC -shared"

    AC_PATH_PROG(AR, ar)
    AC_SUBST(AR)
    ARFLAGS="crv"
    AC_SUBST(ARFLAGS)

elif test $COMPILER_TYPE = "intel" ; then

    CXXFLAGS="$CXXFLAGS -Dcompiler_intel -fPIC"
    CFLAGS="$CFLAGS -Dcompiler_intel -fPIC"
    
    CXXDEBUGFLAGS="-g -O0"
    CDEBUGFLAGS="-g -O0"

    SOFLAGS="$SOFLAGS -fPIC -shared"

    AC_PATH_PROG(AR, ar)
    AC_SUBST(AR)
    ARFLAGS="crv"
    AC_SUBST(ARFLAGS)
fi

dnl === Use OUR debug/optimization levels
if test $debug_mode = "yes" ; then
    CXXFLAGS="$CXXDEBUGFLAGS $CXXFLAGS -DDEBUG"
    CFLAGS="$CDEBUGFLAGS $CFLAGS -DDEBUG"
else
    CXXFLAGS="$CXXOPTFLAGS $CXXFLAGS"
    CFLAGS="$COPTFLAGS $CFLAGS"
fi

dnl === OS-SPECIFIC COMPILER OPTIONS ===
case "$host_os" in
  *linux*)
    LDFLAGS="$LDFLAGS -Wl,-E"
    CN_LDFLAGS="$LDFLAGS"
    os_release=`uname -r`
    case "$os_release" in
      2.4*)
        CXXFLAGS="$CXXFLAGS -Dos_linux=24"
        CFLAGS="$CFLAGS -Dos_linux=24"
        ;;
      2.6*)
        CXXFLAGS="$CXXFLAGS -Dos_linux=26"
        CFLAGS="$CFLAGS -Dos_linux=26"
        ;;
      *)
        CXXFLAGS="$CXXFLAGS -Dos_linux"
        CFLAGS="$CFLAGS -Dos_linux"
        ;;
    esac
    ;;
  *aix*)
    CXXFLAGS="$CXXFLAGS -Dos_aix"
    CFLAGS="$CFLAGS -Dos_aix"
    ;;
  *solaris*)
    LIBS="-lrpcsvc -lsocket -lnsl $LIBS"
    LIBS_LIGHTWEIGHT="-lrpcsvc -lsocket -lnsl $LIBS_LIGHTWEIGHT"

    CXXFLAGS="$CXXFLAGS -Dos_solaris"
    CFLAGS="$CFLAGS -Dos_solaris"
    ;;
  *)
    ;;
esac

LIBS_LOWFAT="$LIBS_LIGHTWEIGHT -lpthread"


dnl === CRAY XT OPTIONS ===
CRAY_STARTUP_TYPE=""

if test $MRNET_STARTUP_METHOD = "ssh" ; then
    AS_IF([test -f "/proc/cray_xt/nid"],[MRNET_STARTUP_METHOD="cray"])
    if test $MRNET_STARTUP_METHOD = "cray" ; then
        echo "checking for /proc/cray_xt/nid... yes"
    else
        echo "checking for /proc/cray_xt/nid... no"
    fi
fi

if test $MRNET_STARTUP_METHOD = "cray" ; then
    CRAY_STARTUP_TYPE="ALPS"
    CRAYXT_ATH_LIBS=""
    CRAYXE_ATH_LIBS=""

    CXXFLAGS="$CXXFLAGS -Darch_crayxt -DBUILD_CRAY_ALPS"
    CFLAGS="$CFLAGS -Darch_crayxt -DBUILD_CRAY_ALPS"

    LDFLAGS_HOLD="$LDFLAGS"

    if test "x$CRAY_ATH_INCDIR" != "x" ; then
        CXXFLAGS="$CXXFLAGS -I$CRAY_ATH_INCDIR"
    fi

    if test "x$CRAY_ATH_LIBDIR" != "x" ; then
        CRAYXT_ATH_LIBS="-L$CRAY_ATH_LIBDIR $CRAYXT_ATH_LIBS"
        CRAYXE_ATH_LIBS="-L$CRAY_ATH_LIBDIR $CRAYXE_ATH_LIBS"
        LDFLAGS="$LDFLAGS -L$CRAY_ATH_LIBDIR"
    fi

    AC_CHECK_HEADERS([alps/alps.h] [alps/alps_toolAssist.h] [alps/apInfo.h], [], 
                     [AC_MSG_ERROR([One of the required ALPS include files was not found: alps/alps.h, alps/alps_toolAssist.h, alps/apInfo.h])])

    HAVE_LIBALPS="no"
    AC_CHECK_HEADERS([alps/libalps.h] [libalps.h], [HAVE_LIBALPS="yes"; break], [], 
[#if HAVE_ALPS_ALPS_TOOLASSIST_H
# include <alps/alps_toolAssist.h>
#endif
#if HAVE_ALPS_APINFO_H 
# include <alps/apInfo.h> 
#endif
]
                    )
    if test $CXX = "CC" ; then
        LDFLAGS="-dynamic $LDFLAGS"
        CN_LDFLAGS="-dynamic $CN_LDFLAGS"
    fi

    if test $HAVE_LIBALPS = "no" ; then
        if test "x$CRAY_ATH_INCDIR" != "x" ; then
            AC_MSG_ERROR(Can't find libalps.h. Specify proper directory with --with-alpstoolhelp-inc)
        fi
    fi

    if test $HAVE_LIBALPS = "yes" ; then

        HAVE_ATH_LIBS="no"
        LIBS_HOLD=$LIBS

        CRAYXT_ATH_LIBS_SO="$CRAYXT_ATH_LIBS -lalps"
        CRAYXT_ATH_LIBS="$CRAYXT_ATH_LIBS -Wl,-Bstatic -lalps -lxmlrpc -Wl,-Bdynamic"
        CRAYXE_ATH_LIBS_SO="$CRAYXE_ATH_LIBS -lalps"
        CRAYXE_ATH_LIBS="$CRAYXE_ATH_LIBS -Wl,-Bstatic -lalps -lxmlrpc-epi $EXPAT_LIB -lexpat -Wl,-Bdynamic"

        AC_CHECK_LIB( [alps], [alps_launch_tool_helper],
                      [HAVE_ATH_LIBS="yes"; EXTRA_LIBS="$CRAYXT_ATH_LIBS $EXTRA_LIBS"; EXTRA_LIBS_SO="$CRAYXT_ATH_LIBS_SO $EXTRA_LIBS_SO"],
                      [], [-lxmlrpc] )

        if test $HAVE_ATH_LIBS = "no" ; then
            unset ac_cv_lib_alps_alps_launch_tool_helper
            LIBS="$LIBS_HOLD"
            AC_CHECK_LIB( [alps], [alps_launch_tool_helper],
                          [HAVE_ATH_LIBS="yes"; EXTRA_LIBS="$CRAYXE_ATH_LIBS $EXTRA_LIBS"; EXTRA_LIBS_SO="$CRAYXE_ATH_LIBS_SO $EXTRA_LIBS_SO"],
                          [], [-lxmlrpc-epi -lexpat] )
        fi

        LIBS="$LIBS_HOLD"

        if test $HAVE_ATH_LIBS = "no" ; then
            if test "x$CRAY_ATH_INCDIR" != "x" ; then
                AC_MSG_ERROR(Can't find libalps. Specify proper directory with --with-alpstoolhelp-lib)
            fi
        fi
    fi
    
    LDFLAGS="$LDFLAGS_HOLD"
fi


if test $MRNET_STARTUP_METHOD = "cray-cti" ; then
    CRAY_STARTUP_TYPE="CTI"
    CRAYXE_CTI_LIBS=""

    CXXFLAGS="$CXXFLAGS -Darch_crayxt -DBUILD_CRAY_CTI"
    CFLAGS="$CFLAGS -Darch_crayxt -DBUILD_CRAY_CTI"

    LDFLAGS_HOLD="$LDFLAGS"

    if test "x$CRAY_CTI_INCDIR" != "x" ; then
        CXXFLAGS="$CXXFLAGS -I$CRAY_CTI_INCDIR"
    fi

    if test "x$CRAY_CTI_LIBDIR" != "x" ; then
        CRAYXT_CTI_LIBS="-L$CRAY_CTI_LIBDIR $CRAYXT_CTI_LIBS"
        CRAYXE_CTI_LIBS="-L$CRAY_CTI_LIBDIR $CRAYXE_CTI_LIBS"
        LDFLAGS="$LDFLAGS -L$CRAY_CTI_LIBDIR -ldl -lrt -lpthread"
    fi

    AC_CHECK_HEADERS([common_tools_fe.h], [], 
                     [AC_MSG_ERROR([Could not find CTI header common_tools_fe.h])])



    if test $CXX = "CC" ; then
        LDFLAGS="-dynamic $LDFLAGS"
        CN_LDFLAGS="-dynamic $CN_LDFLAGS"
    fi


    HAVE_CTI_LIBS="no"
    LIBS_HOLD=$LIBS
    CRAYXT_CTI_LIBS_SO="$CRAYXT_CTI_LIBS -lcommon_tools_fe -lcommon_tools_be"
    CRAYXT_CTI_LIBS="$CRAYXT_CTI_LIBS_SO"
    CRAYXE_CTI_LIBS_SO="$CRAYXE_CTI_LIBS -lcommon_tools_fe -lcommon_tools_be"
    CRAYXE_CTI_LIBS="$CRAYXE_CTI_LIBS_SO"

    AC_CHECK_LIB( [common_tools_fe], [cti_version],
                  [HAVE_CTI_LIBS="yes"; EXTRA_LIBS="$CRAYXT_CTI_LIBS $EXTRA_LIBS"; EXTRA_LIBS_SO="$CRAYXT_CTI_LIBS_SO $EXTRA_LIBS_SO"],
                  [], [-ldl -lrt -lpthread] )

    if test $HAVE_CTI_LIBS = "no" ; then
        unset ac_cv_lib_alps_alps_launch_tool_helper
        LIBS="$LIBS_HOLD"
        AC_CHECK_LIB( [common_tools_fe], [cti_version],
                      [HAVE_CTI_LIBS="yes"; EXTRA_LIBS="$CRAYXE_CTI_LIBS $EXTRA_LIBS"; EXTRA_LIBS_SO="$CRAYXE_CTI_LIBS_SO $EXTRA_LIBS_SO"],
                      [], [-ldl -lrt -lpthread] )
    fi

    LIBS="$LIBS_HOLD"

    if test $HAVE_CTI_LIBS = "no" ; then
        if test "x$CRAY_CTI_INCDIR" != "x" ; then
            AC_MSG_ERROR(Can't find common_tools_fe. Specify proper directory with --with-alpstoolhelp-lib)
        fi
    fi
    
    LDFLAGS="$LDFLAGS_HOLD"
fi



if test $MRNET_STARTUP_METHOD = "libi" ; then
	if test $LIBI_STARTUP_METHOD = "slurm" ; then
		LIBLMON="-L$LMON_IDIR/lib -lmonfeapi -lmonmwapi -lmonbeapi"
	fi
fi
AC_SUBST(LIBLMON)

AC_SUBST(MRNET_STARTUP_METHOD)

AC_MSG_CHECKING([[whether we can compile/link with "$CXX $CXXFLAGS $LIBS"]])
AC_LINK_IFELSE( [AC_LANG_PROGRAM([],[])],
                [AC_MSG_RESULT(yes)],
                [AC_MSG_ERROR(no)])

AC_SUBST(COMPILER_TYPE)
AC_SUBST(CXX)
AC_SUBST(CXXFLAGS)
AC_SUBST(CC)
AC_SUBST(CFLAGS)
AC_SUBST(LDFLAGS)
AC_SUBST(CN_LDFLAGS)
AC_SUBST(SOFLAGS)
AC_SUBST(C_AS_CPLUSPLUS)
AC_SUBST(LIBS)
AC_SUBST(LIBS_LIGHTWEIGHT)
AC_SUBST(LIBS_LOWFAT)
AC_SUBST(EXTRA_LIBS)
AC_SUBST(EXTRA_LIBS_SO)
AC_SUBST(EXTRA_LIBS_LIGHTWEIGHT)
AC_SUBST(EXTRA_LIBS_LOWFAT)
AC_SUBST(LTWT_THREADSAFE)
AC_SUBST(CRAY_STARTUP_TYPE)

AC_C_BIGENDIAN
AC_CHECK_SIZEOF(char)
AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(float)
AC_CHECK_SIZEOF(double)

PLATFORM_BUILD="`pwd`/build/${host}"
AC_SUBST(PLATFORM_BUILD)
AS_MKDIR_P([${PLATFORM_BUILD}/bin])
AS_MKDIR_P([${PLATFORM_BUILD}/depends/ltwt])
AS_MKDIR_P([${PLATFORM_BUILD}/depends/xplat/ltwt])
AS_MKDIR_P([${PLATFORM_BUILD}/lib])
AS_MKDIR_P([${PLATFORM_BUILD}/obj/ltwt])
AS_MKDIR_P([${PLATFORM_BUILD}/obj/xplat/ltwt])

AC_CONFIG_HEADER([${PLATFORM_BUILD}/mrnet_config.h:conf/config.h.in])
AC_CONFIG_HEADER([${PLATFORM_BUILD}/xplat_config.h:xplat/conf/config.h.in])
AC_CONFIG_FILES([Makefile:conf/Makefile_tld.in ${PLATFORM_BUILD}/Makefile:conf/Makefile.in ${PLATFORM_BUILD}/Makefile.ltwt:conf/Makefile.ltwt.in ${PLATFORM_BUILD}/Makefile.xplat:xplat/conf/Makefile.in ${PLATFORM_BUILD}/Makefile.xplat_ltwt:xplat/conf/Makefile.ltwt.in ${PLATFORM_BUILD}/Makefile.examples:conf/Makefile.examples.in])

if test $MRNET_STARTUP_METHOD = "libi" ; then
    AC_CONFIG_SUBDIRS([external/libi])
fi

AC_OUTPUT
